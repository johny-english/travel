#!/usr/bin/fish

argparse "env=" "dry_run" "destination=" -- $argv || exit 1

function execute
   if set -q _flag_dry_run
      echo "NOT Executing: $argv"
   else
      echo "Executing: $argv"
      eval $argv
   end
end

function cp_supports_update_none
  cp  --update=none --help &>/dev/null
end

# First arg has to be set:
set -l env_fish $_flag_env
if test -z env_fish
   set -l this_script (realpath (status current-filename))
   set -l this_dir (dirname $this_script)
   echo "Usage: $this_script <env.fish>, for example:"
   for f in $this_dir/env_*.fish
      set -l relpath (realpath --relative-to=$this_dir $f)
      echo "   $this_script $relpath"
   end
   exit 1
end


# set fish_trace 2



function discover --argument-names path prefix
   echo "$prefix$path:" >&2
   echo (realpath $path)
   if not string  match -qr ".*\.fish\$" $path # Allow fish scripts to "install" python/json/etc... (without breaking source $path)
      return
   end
   source $path
   set discovered (impl_mixin_deps)
   for f in $discovered
      discover $f "$prefix   "
   end
end

# Grab all info from the provided env file:
source $env_fish

 # Pick destination either from command-line arg or from impl_fish_home_path
if test -n "$_flag_destination"
   set fish_install_dir $_flag_destination
else
   set fish_install_dir (realpath -m (impl_fish_home_path)/.config/fish/)
end

set -l config_fish_files (impl_config_fish)
set -l env_json_cfg (impl_get_install_fish_info)[3]
mkdir -p $fish_install_dir/conf.d
echo "Installing from "(set_color -o brwhite)$env_fish(set_color normal)" to "(set_color -o brwhite)$fish_install_dir(set_color normal)":"
echo

# Main:
echo (set_color -o bryellow)Recursively discovering files to install:(set_color normal)
set files_to_install (discover $env_fish "   " | sort -u)

echo
echo (set_color -o bryellow)Installing...(set_color normal)
for f in $files_to_install
   if cp_supports_update_none
      execute cp --update=none $f $fish_install_dir/conf.d
   else
      execute cp -n $f $fish_install_dir/conf.d
   end
end
execute "echo \"# This is an equivalent of .bashrc\" >> $fish_install_dir/config.fish"
execute "echo >> $fish_install_dir/config.fish"
for f in $config_fish_files
   execute "echo \"# Autogenerated from $f:\" >> $fish_install_dir/config.fish"
   execute "cat $f >> $fish_install_dir/config.fish"
   execute "echo >> $fish_install_dir/config.fish"
end


# Rename environment json file to settings.json (this is what json_settings.fish will be looking for)
set fish_trace 1
if test -n "$env_json_cfg"
   cp $env_json_cfg $fish_install_dir/conf.d/settings.json
end
